plugins {
    id 'java-library'
    id 'maven-publish'
    id 'idea'
    id 'net.neoforged.moddev' version '2.0.116'
}

version = mod_version
group = mod_group_id

repositories {
    mavenLocal()
}

jar {
    from(project.file("LICENSE")) {
        rename { "${mod_id}_${it}" }
    }
    from(project.file("README.md")) {
        rename { "${mod_id}_${it}" }
    }
}

base {
    archivesName = mod_name
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

neoForge {
    version = project.neo_version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_version
    }

    runs {
        client {
            client()
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        gameTestServer {
            type = "gameTestServer"
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()

            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenCentral()
    maven {
        name = "Cjsah Maven"
        url = "https://server.cjsah.net:1002/maven/"
    }
    maven {
        name = "BlameJared"
        url "https://maven.blamejared.com"
    }
    maven {
        name = "Shedaniel Maven"
        url "https://maven.shedaniel.me/"
    }
    maven {
        name = "Geckolib Examples"
        url "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/"
        content {
            includeGroup("software.bernie.geckolib")
        }
    }
    maven {
        name = "Wisp Forest"
        url "https://maven.wispforest.io/releases/"
    }
    maven {
        url "https://maven.su5ed.dev/releases"
    }
    maven {
        url "https://modmaven.dev/"
        content {
            includeGroup "dev.technici4n"
        }
    }
    maven {
        url = 'https://nexus.resourcefulbees.com/repository/maven-public/'
        content {
            includeGroup 'earth.terrarium.athena'
        }
    }
    maven {
        name = "Modrinth Maven"
        url "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
    maven {
        name = "CurseForge Maven"
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    implementation "rearth.oritech:oritech-neoforge-${mc_version}:${oritech_version}"

    api "com.tterrag.registrate:Registrate:${registrate_version}"
    jarJar "com.tterrag.registrate:Registrate:${registrate_version}"

    runtimeOnly "curse.maven:oritechthings-1166849:${oritech_things_version}"

}

var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [minecraft_version      : mc_version,
                             minecraft_version_range: mc_version_range,
                             neo_version            : neo_version,
                             neo_version_range      : neo_version_range,
                             oritech_version_range  : oritech_version_range,
                             loader_version_range   : loader_version_range,
                             mod_id                 : mod_id,
                             mod_name               : mod_name,
                             mod_license            : mod_license,
                             mod_version            : mod_version,
                             mod_authors            : mod_authors,
                             mod_description        : mod_description]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}

sourceSets.main.resources.srcDir generateModMetadata
neoForge.ideSyncTask generateModMetadata

processResources {
    exclude '**/*.psd'
}

// Example configuration to allow publishing using the maven-publish plugin
publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
